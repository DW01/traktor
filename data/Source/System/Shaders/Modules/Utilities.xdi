<?xml version="1.0" encoding="utf-8"?>
<object type="traktor.render.ShaderModule">
	<text>
	<![CDATA[
const float PI = 3.142f;
const float TWO_PI = PI * 2.0f;
const float HALF_PI = PI / 2.0f;

// Check if X and Y are within [0;max] range.
bool CheckRange(ivec2 p, ivec2 mx)
{
	if (p.x < 0 || p.y < 0)
		return false;
	else if (p.x >= mx.x || p.y >= mx.y)
		return false;
	else
		return true;
}

vec4 axisX(mat4 m)
{
	return m[0];
}

vec4 axisY(mat4 m)
{
	return m[1];
}

vec4 axisZ(mat4 m)
{
	return m[2];
}

float lerpXY(vec4 v, float f)
{
	return mix(v.x, v.y, f);
}

int MajorAxis(vec3 v)
{
	const vec3 av = abs(v);
	if (av.x > av.y)
		return (av.x > av.z) ? 0 : 2;
	else
		return (av.y > av.z) ? 1 : 2;
}

void OrthogonalFrame(vec3 d, out vec3 u, out vec3 v)
{
	const vec3 c_axises[] =
	{
		vec3(0.0f, 1.0f, 0.0f),
		vec3(0.0f, 0.0f, 1.0f),
		vec3(1.0f, 0.0f, 0.0f)
	};

	const int major = MajorAxis(d);
	u = normalize(cross(d, c_axises[major]));
	v = normalize(cross(u, d));
}

mat3 Rotate(vec3 axis, float angle)
{
	const float c = cos(angle);
	const float s = sin(angle);
	const float t = 1.0f - c;
	const vec3 txyz = t * axis;
	return mat3(
		txyz.x * axis.x + c, txyz.x * axis.y + s * axis.z, txyz.x * axis.z - s * axis.y,
		txyz.x * axis.y - s * axis.z, txyz.y * axis.y + c, txyz.y * axis.z + s * axis.x,
		txyz.x * axis.z + s * axis.y, txyz.y * axis.z - s * axis.x, txyz.z * axis.z + c
	);
}

vec3 Sphere(vec2 rnd)
{
	const float z = 2.0f * rnd.x - 1.0f;
	const float t = 2.0f * 3.142f * rnd.y;
	const float w = sqrt(1.0f - z * z);
	const float x = w * cos(t);
	const float y = w * sin(t);
	return vec3(x, y, z);
}

vec3 Hemi(vec3 direction, vec2 rnd)
{
	vec3 ot = Sphere(rnd);
	if (dot(ot, direction) < 0.0f)
		ot = -ot;
	return ot;
}


	]]>
	</text>
</object>
